plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.0'
}

group = 'com.azk.pulse'
version = '0.1.0'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
    implementation 'org.xerial:sqlite-jdbc:3.46.1.0'
    implementation 'com.mysql:mysql-connector-j:8.3.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.shadowJar {
    archiveBaseName.set('Pulse')
    archiveClassifier.set('')
    archiveVersion.set(version.toString())
}

tasks.build {
    dependsOn tasks.shadowJar
}

tasks.jar {
    enabled = false
}

tasks.processResources {
    def props = [version: version]
    inputs.properties(props)
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand(props)
    }
}
